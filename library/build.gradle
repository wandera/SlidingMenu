apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

def packageName = 'com.wandera.android'

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.google.com' }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.5.4"
    }
}

repositories {
    google()
}

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 26
        archivesBaseName = "sliding-menu"
        version "1.4"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }

    sourceSets {
        main {
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']

            manifest.srcFile 'AndroidManifest.xml'
        }
    }

    libraryVariants.all { variant ->
        println "variant: " + variant.baseName
        variant.outputs.all { output ->
            if (outputFile != null && outputFileName.endsWith('.aar')) {
                // LibraryVariant object does not have #versionNameSuffix
                def suffix = variant.baseName == "debug" ? "-SNAPSHOT" : ""
                outputFileName = "${archivesBaseName}-${version}${suffix}.aar"
                println "outputFileName: " + outputFileName
            }
        }
    }

}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.annotation:annotation:1.1.0-rc01'
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId = packageName
            artifactId = archivesBaseName
            def targetFile = "$buildDir/outputs/aar/${archivesBaseName}-${version}.aar"
            println "publish source: " + targetFile
            // Tell maven to prepare the generated "*.aar" file for publishing
            artifact(targetFile)
        }
    }
}

